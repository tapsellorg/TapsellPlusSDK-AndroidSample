apply plugin: 'com.android.application'

android {
    namespace 'ir.tapsell.plussample.android'
    compileSdkVersion 33
    defaultConfig {
        applicationId "ir.tapsell.plussample.android"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "String", "TAPSELL_KEY", "\"alsoatsrtrotpqacegkehkaiieckldhrgsbspqtgqnbrrfccrtbdomgjtahflchkqtqosa\""
        buildConfigField "String", "TAPSELL_REWARDED_VIDEO", "\"5cfaa802e8d17f0001ffb28e\""
        buildConfigField "String", "TAPSELL_INTERSTITIAL", "\"5cfaa942e8d17f0001ffb292\""
        buildConfigField "String", "TAPSELL_NATIVE_BANNER", "\"5cfaa9deaede570001d5553a\""
        buildConfigField "String", "TAPSELL_NATIVE_VIDEO", "\"60edcf8ad3459c17f019d36b\""
        buildConfigField "String", "TAPSELL_STANDARD_BANNER", "\"5cfaaa30e8d17f0001ffb294\""
        buildConfigField "String", "TAPSELL_VAST_PREROLL", "\"60e441ff537bfb4073746249\""
    }
    signingConfigs {
        release {
            if (project.hasProperty('MYAPP_RELEASE_STORE_FILE')) {
                storeFile file(MYAPP_RELEASE_STORE_FILE)
                storePassword MYAPP_RELEASE_STORE_PASSWORD
                keyAlias MYAPP_RELEASE_KEY_ALIAS
                keyPassword MYAPP_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'

    // for TapsellPlus
    implementation 'ir.tapsell.plus:tapsell-plus-sdk-android:2.2.0'

    //for adMob
    implementation 'com.google.android.gms:play-services-ads:21.5.0'

    //for unityAds
    implementation 'com.unity3d.ads:unity-ads:4.6.1'

    //for chartboost
    implementation 'com.chartboost:chartboost-sdk:8.2.1'

    //for applovin
    implementation 'com.applovin:applovin-sdk:11.8.2'

    //for adcolony
    implementation 'com.adcolony:sdk:4.8.0'

    implementation ("com.google.android.gms:play-services-base:18.2.0"){
        exclude group: 'com.android.support'
    }
    implementation ("com.google.android.gms:play-services-ads-identifier:18.0.1"){
        exclude group: 'com.android.support'
    }

    // For Mintegral - NOTE: Add custom repository (explained after this)
    implementation "com.mbridge.msdk.oversea:videojs:16.3.91"
    implementation "com.mbridge.msdk.oversea:mbbanner:16.3.91"
    implementation "com.mbridge.msdk.oversea:mbjscommon:16.3.91"
    implementation "com.mbridge.msdk.oversea:playercommon:16.3.91"
    implementation "com.mbridge.msdk.oversea:reward:16.3.91"
    implementation "com.mbridge.msdk.oversea:videocommon:16.3.91"
    implementation "com.mbridge.msdk.oversea:same:16.3.91"
    implementation "com.mbridge.msdk.oversea:interstitialvideo:16.3.91"

    implementation 'com.google.android.material:material:1.9.0'

    // for Vast (PreRoll)
    implementation("com.google.ads.interactivemedia.v3:interactivemedia:3.30.1")
    implementation 'com.google.android.exoplayer:exoplayer-core:2.18.7'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.18.7'

    testImplementation 'junit:junit:4.13.2'

    // If you face with duplicated class error
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
